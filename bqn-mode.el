;;; bqn-mode.el --- Emacs mode for BQN -*- lexical-binding: t -*-

;; Emacs bqn-mode is derived from gnu-apl-mode,
;; which is copyright 2013-2015 Elias M√•rtenson <lokedhs@gmail.com>.
;; Changes are copyright 2021 Marshall Lochbaum <mwlochbaum@gmail.com>.

;; Author: Marshall Lochbaum <mwlochbaum@gmail.com>
;; Version: 0.1.0
;; Package-Requires: ((emacs "26.1"))
;; URL: https://github.com/museoa/bqn-mode
;; SPDX-License-Identifier: GPL-3.0-or-later

;;; Commentary:

;; There are two ways to access the BQN keymap:
;; - When editing a BQN file, use keys with the super (s-) modifier.
;; - Enable backslash prefixes by entering C-\ (`toggle-input-method')
;;   then BQN-Z. Then enter backslash \ before a key.

;;; Code:

(require 'cl-lib)
(require 'comint)
(require 'quail)
(require 'bqn-symbols-doc)

;;;###autoload
(defgroup bqn nil
  "Major mode for interacting with the BQN interpreter."
  :prefix 'bqn
  :group 'languages)

(defvar bqn-symbols--list
  '(;; Top row
    ;; `
    ("high-tilde" "Àú" ?`)
    ("logical-not" "¬¨" ?~)
    ;; 1
    ("breve" "Àò" ?1)
    ("circle-notch" "‚éâ" ?!)
    ;; 2
    ("diaeresis" "¬®" ?2)
    ("circled-diaeresis" "‚öá" ?@)
    ;; 3
    ("high-equals" "‚Åº" ?3)
    ("circle-star" "‚çü" ?#)
    ;; 4
    ("corner" "‚åú" ?4)
    ("circle-corner" "‚ó∂" ?$)
    ;; 5
    ("acute" "¬¥" ?5)
    ("circle-slash" "‚äò" ?%)
    ;; 6
    ("double-acute" "Àù" ?6)
    ("circle-triangle" "‚éä" ?^)
    ;; 7
    ("hydrant" "‚çé" ?&)
    ;; 8
    ("infinity" "‚àû" ?8)
    ("thorn" "‚çï" ?*)
    ;; 9
    ("high-minus" "¬Ø" ?9)
    ("open-angle" "‚ü®" ?\()
    ;; 0
    ("bullet" "‚Ä¢" ?0)
    ("close-angle" "‚ü©" ?\))
    ;; -
    ("division-sign" "√∑" ?-)
    ("root" "‚àö" ?_)
    ;; =
    ("multiplication-sign" "√ó" ?=)
    ("star" "‚ãÜ" ?+)

    ;; First row
    ;; q
    ("circle-stile" "‚åΩ" ?q)
    ("down-left-arrow" "‚Üô" ?Q)
    ;; w
    ("double-w" "ùï®" ?w)
    ("double-upper-w" "ùïé" ?W)
    ;; e
    ("epsilon" "‚àä" ?e)
    ("epsilon-underbar" "‚ç∑" ?E)
    ;; r
    ("up-arrow" "‚Üë" ?r)
    ("double-r" "ùï£" ?R)
    ;; t
    ("logical-and" "‚àß" ?t)
    ("up-grade" "‚çã" ?T)
    ;; y
    ;; u
    ("square-cup" "‚äî" ?u)
    ;; i
    ("open-square" "‚äè" ?i)
    ("open-square-underbar" "‚äë" ?I)
    ;; o
    ("close-square" "‚äê" ?o)
    ("close-square-underbar" "‚äí" ?O)
    ;; p
    ("pi" "œÄ" ?p)
    ("iota" "‚ç≥" ?P)
    ;; [
    ("left-arrow" "‚Üê" ?\[)
    ("left-tack" "‚ä£" ?{)
    ;; ]
    ("right-arrow" "‚Üí" ?\])
    ("right-tack" "‚ä¢" ?})
    ;; \
    ("backslash" "\\" ?\\)

    ;; Second row
    ;; a
    ("circle-backslash" "‚çâ" ?a)
    ("up-left-arrow" "‚Üñ" ?A)
    ;; s
    ("double-s" "ùï§" ?s)
    ("double-upper-s" "ùïä" ?S)
    ;; d
    ("up-down-arrow" "‚Üï" ?d)
    ;; f
    ("double-f" "ùïó" ?f)
    ("double-upper-f" "ùîΩ" ?F)
    ;; g
    ("double-g" "ùïò" ?g)
    ("double-upper-g" "ùîæ" ?G)
    ;; h
    ("left-loop" "‚ä∏" ?h)
    ("left-chevron" "¬´" ?H)
    ;; j
    ("jot" "‚àò" ?j)
    ;; k
    ("circle" "‚óã" ?k)
    ("circle-jot" "‚åæ" ?K)
    ;; l
    ("right-loop" "‚üú" ?l)
    ("right-chevron" "¬ª" ?L)
    ;; ;
    ("diamond" "‚ãÑ" ?\;)
    ("middle-dot" "¬∑" ?:)
    ;; '
    ("left-hook-arrow" "‚Ü©" ?')
    ("high-dot" "Àô" ?\")

    ;; Third row
    ;; z
    ("zigzag" "‚•ä" ?z)
    ("bowtie" "‚ãà" ?Z)
    ;; x
    ("double-x" "ùï©" ?x)
    ("double-upper-x" "ùïè" ?X)
    ;; c
    ("down-arrow" "‚Üì" ?c)
    ;; v
    ("logical-or" "‚à®" ?v)
    ("down-grade" "‚çí" ?V)
    ;; b
    ("left-floor" "‚åä" ?b)
    ("left-ceiling" "‚åà" ?B)
    ;; n
    ;; m
    ("identical-to" "‚â°" ?m)
    ("not-identical-to" "‚â¢" ?M)
    ;; ,
    ("join" "‚àæ" ?,)
    ("less-than-or-equal-to" "‚â§" ?<)
    ;; .
    ("couple" "‚âç" ?.)
    ("greater-than-or-equal-to" "‚â•" ?>)
    ;; /
    ("not-equal-to" "‚â†" ?/)
    ("left-double-arrow" "‚áê" ??)

    ;; Space bar
    ("ligature" "‚Äø" ? )))

;;;; input method

(quail-define-package "BQN-Z" "UTF-8" "‚çâ" t
                      "Input mode for BQN"
                      '(("\t" . quail-completion))
                      t                 ; forget-last-selection
                      nil               ; deterministic
                      nil               ; kbd-translate
                      t                 ; show-layout
                      nil               ; create-decode-map
                      nil               ; maximum-shortest
                      nil               ; overlay-plist
                      nil               ; update-translation-function
                      nil               ; conversion-keys
                      t                 ; simple
                      )

(defvar bqn-key-prefix--transcription-alist)
(defun bqn-key-prefix--set (prefix new)
  "Set a key PREFIX to the NEW one."
  (quail-select-package "BQN-Z")
  (quail-install-map
   (let* ((prefix (string new))
          (bqn-key-prefix--transcription-alist
           (cl-loop for command in bqn-symbols--list
                    collect (cons (concat prefix (char-to-string (cl-third command)))
                                  (cl-second command)))))
     (quail-map-from-table
      '((default bqn-key-prefix--transcription-alist)))))
  (set-default prefix new))

(defun bqn-key-prefix--initialize (prefix new)
  "Initialize the key PREFIX with the NEW one."
  (custom-initialize-default prefix new)
  (bqn-key-prefix--set prefix (eval new)))

(defcustom bqn-key-prefix ?\\
  "Set a character to serve as prefix key for BQN symbol input."
  :type 'character
  :group 'bqn
  :initialize #'bqn-key-prefix--initialize
  :set #'bqn-key-prefix--set)

;;;; core functionality

(defface bqn-default
  '((t (:family "BQN386 Unicode")))
  "Default face for BQN source and inferior-process buffers."
  :group 'bqn)

(defface bqn-syntax-block-face
  '((t (:inherit font-lock-function-name-face)))
  "Face used for BQN curly braces."
  :group 'bqn)

(defface bqn-syntax-paren-face
  '((t (:inherit default)))
  "Face used for BQN parentheses."
  :group 'bqn)

(defface bqn-syntax-header-face
  '((t (:inherit default)))
  "Face used for BQN header delimiters : and ; ."
  :group 'bqn)

(defface bqn-syntax-list-face
  '((t (:inherit font-lock-builtin-face)))
  "Face used for BQN list characters: angle brackets and ligature."
  :group 'bqn)

(defface bqn-syntax-separator-face
  '((t (:inherit font-lock-builtin-face)))
  "Face used for BQN expression separators."
  :group 'bqn)

(defface bqn-syntax-arrow-face
  '((t (:inherit 'default)))
  "Face used for BQN assignment and return arrows."
  :group 'bqn)

(defface bqn-syntax-function-face
  '((t (:inherit font-lock-type-face)))
  "Face used for BQN functions."
  :group 'bqn)

(defface bqn-syntax-one-modifier-face
  '((t (:inherit font-lock-preprocessor-face)))
  "Face used for BQN 1-modifiers."
  :group 'bqn)

(defface bqn-syntax-two-modifier-face
  '((t (:inherit font-lock-keyword-face)))
  "Face used for BQN 2-modifiers."
  :group 'bqn)

(defface bqn-syntax-subject-face
  '((t (:inherit font-lock-variable-name-face)))
  "Face used for BQN subjects."
  :group 'bqn)

(defface bqn-syntax-nothing-face
  '((t (:inherit font-lock-constant-face)))
  "Face used for BQN Nothing (¬∑)."
  :group 'bqn)

(defface bqn-syntax-number-face
  '((t (:inherit font-lock-constant-face)))
  "Face used for BQN numeric literals."
  :group 'bqn)

(defvar bqn-syntax--token-types
  '((("'.'\\|@" . font-lock-string-face)
     ("[{}]" . 'bqn-syntax-block-face)
     ("[()]" . 'bqn-syntax-paren-face)
     ("[:;?]" . 'bqn-syntax-header-face)
     ("[‚ü®‚ü©‚Äø]" . 'bqn-syntax-list-face)
     ("[‚ãÑ,]" . 'bqn-syntax-separator-face)
     ("[‚Üê‚áê‚Ü©‚Üí]" . 'bqn-syntax-arrow-face)
     ("¬∑" . 'bqn-syntax-nothing-face)
     ("[ùîΩùîæùïéùïèùïä+√ó√∑‚ãÜ‚àö‚åä‚åà|¬¨‚àß‚à®<>‚â†=‚â§‚â•‚â°‚â¢‚ä£‚ä¢‚•ä‚àæ‚âç‚ãà‚Üë‚Üì‚Üï¬´¬ª‚åΩ‚çâ/‚çã‚çí‚äè‚äë‚äê‚äí‚àä‚ç∑‚äî!‚çï‚çé-]\\|‚Ä¢?\\_<[A-Z][A-Z_a-z0-9œÄ‚àû¬Ø]*\\_>" . 'bqn-syntax-function-face)
     ("[ÀôÀúÀò¬®‚åú‚Åº¬¥Àù`]\\|‚Ä¢?\\_<_[A-Za-z][A-Z_a-z0-9œÄ‚àû¬Ø]*\\|_ùï£\\_>" . 'bqn-syntax-one-modifier-face)
     ("[‚àò‚óã‚ä∏‚üú‚åæ‚äò‚ó∂‚éâ‚öá‚çü‚éä]\\|_ùï£_\\|‚Ä¢?\\_<_[A-Za-z][A-Z_a-z0-9œÄ‚àû¬Ø]*_\\_>" . 'bqn-syntax-two-modifier-face)
     ("[ùïóùïòùï®ùï©ùï§ùï£]\\|‚Ä¢\\|‚Ä¢?\\_<[a-z][A-Z_a-z0-9œÄ‚àû¬Ø]*\\_>" . 'bqn-syntax-subject-face)
     ("\\_<¬Ø?\\(\\([0-9]+\\.\\)?[0-9]+\\(e¬Ø?[0-9]+\\)?\\|œÄ\\|‚àû\\)\\(i¬Ø?\\(\\([0-9]+\\.\\)?[0-9]+\\(e¬Ø?[0-9]+\\)?\\|œÄ\\|‚àû\\)\\)?\\_>" . 'bqn-syntax-number-face)
     ("[^ \r\n]" . 'error))
    nil nil nil))

(defvar bqn-syntax--table
  (let ((table (make-syntax-table)))
    (cl-loop for s in bqn-symbols--list
             do (modify-syntax-entry (aref (cl-second s) 0) "." table))
    (cl-loop for s in (append "$%&*+-/<=>|" nil)
             do (modify-syntax-entry s "." table))
    (modify-syntax-entry ?#  "<" table)
    (modify-syntax-entry ?\n ">" table)
    (modify-syntax-entry ?¬Ø  "_" table)
    (modify-syntax-entry ?œÄ  "_" table)
    (modify-syntax-entry ?‚àû  "_" table)
    (modify-syntax-entry ?‚ü©  ")‚ü®" table)
    (modify-syntax-entry ?‚ü®  "(‚ü©" table)
    table)
  "Syntax table for `bqn-mode'.")

;; Eldoc functions
(defvar bqn-help--function-regexp
  (regexp-opt
   (let* ((symbols (mapcar #'cadr bqn-symbols--list))
          (others  (bqn-symbols-doc--symbols)))
     (cl-union symbols others)))
  "Regex to match BQN functions.")

(defun bqn-help--eldoc ()
  "Return the doc string for the thing at point, or nil."
  (when (looking-at bqn-help--function-regexp)
    (bqn-symbols-doc-get-short-doc (match-string 0))))

;; Help functions
(defun bqn-help--close-documentation-buffer ()
  "Close the active documentation window."
  (interactive)
  (quit-window))

(defvar bqn-help-documentation-mode-map
  (let ((map (make-sparse-keymap)))
    (define-key map (kbd "q") 'bqn-help--close-documentation-buffer)
    map)
  "Keymap for documentation buffers.")

(define-derived-mode bqn-help-documentation-mode fundamental-mode
  "BQN Documentation"
  "Major mode for displaying BQN documentation."
  (use-local-map bqn-help-documentation-mode-map)
  (setq-local font-lock-defaults bqn-syntax--token-types)
  (setq-local eldoc-documentation-function 'bqn-help--eldoc)
  (buffer-face-set 'bqn-default))

;; General interactive help
(defvar bqn-help-*documentation-buffer-name* "*bqn-help*")

(defun bqn-help--bqn-symbols-info (test)
  "Find entry in `bqn-symbols--list' using the filter functionp TEST."
  (seq-filter test bqn-symbols--list))

(defun bqn-help-symbol-at-point-is-called ()
  "Show the canonical name of the symbol at point in the minibuffer."
  (interactive)
  (when (looking-at bqn-help--function-regexp)
    (if-let* ((symbol (match-string 0))
              (result (bqn-help--bqn-symbols-info
                       (lambda (v) (equal (cadr v) symbol)))))
        (message "We call symbol %s %s" symbol (caar result))
      (message "No name for %s found" symbol))))

(defun bqn-help-symbol-info-at-point ()
  "Get multi-line documentation for the thing at point, or nil."
  (interactive)
  (when (looking-at bqn-help--function-regexp)
    (if-let* ((symbol (match-string 0))
              (long   (bqn-symbols-doc-get-long-doc symbol))
              (extra  (bqn-symbols-doc-get-extra-doc symbol))
              (sep    "\n\n========================================\n\n")
              (doc-buffer (get-buffer-create
                           bqn-help-*documentation-buffer-name*)))
        (with-current-buffer doc-buffer ;; we have a hit
          (read-only-mode 0)            ; set read only
          (delete-region (point-min) (point-max))
          (insert long sep extra)
          (goto-char (point-min))
          (bqn-help-documentation-mode)
          (read-only-mode 1)            ; unset read only
          (pop-to-buffer doc-buffer))
      (message "No help for %s found!" symbol))))

(defun bqn-mode-map--command-name (name)
  "Return a symbol for the command NAME."
  (intern (concat "bqn-insert-sym-" name)))

(pcase-dolist (`(,name ,symbol ,_) bqn-symbols--list)
  (defalias (bqn-mode-map--command-name name)
    (lambda ()
      (interactive)
      (insert symbol))))

(defun bqn-mode-map--make-base (prefix)
  "Create a new keymap using the key PREFIX."
  (let ((map (make-sparse-keymap)))
    (pcase-dolist (`(,name ,_ ,key) bqn-symbols--list)
      (let ((cmd (bqn-mode-map--command-name name))
            (key (single-key-description key)))
        (define-key map (kbd (concat prefix key)) cmd)))
    (define-key map [menu-bar bqn] (cons "BQN" (make-sparse-keymap "BQN")))
    map))

;; value gets updated by initialization of `bqn-mode-map-prefix'
(defvar bqn-mode-map--keymap nil
  "The keymap for `bqn-mode'.")

(defun bqn-mode-map--set-prefix (prefix new)
  "Set the BQN mode keymap's PREFIX to NEW and recreate the keymap."
  (set-default prefix new)
  (setq bqn-mode-map--keymap (bqn-mode-map--make-base new)))

(defcustom bqn-mode-map-prefix "s-"
  "This stores the keymap prefix for `bqn-mode-map--keymap'.
It is used both to store the new value using
`set-create' and to update `bqn-mode-map--keymap' using
`bqn-mode-map--make-base'. Kill and restart your BQN buffers
to reflect the change."
  :type 'string
  :group 'bqn
  :set #'bqn-mode-map--set-prefix
  :initialize #'custom-initialize-set)

;;;###autoload
(define-derived-mode bqn-mode prog-mode "BQN"
  "Major mode for editing BQN files."
  :syntax-table bqn-syntax--table
  :group 'bqn
  (use-local-map bqn-mode-map--keymap)
  (setq-local font-lock-defaults bqn-syntax--token-types)
  (setq-local eldoc-documentation-function 'bqn-help--eldoc)
  (setq-local comment-start "# ")
  (buffer-face-set 'bqn-default))

;;;###autoload
(add-to-list 'auto-mode-alist '("\\.bqn\\'" . bqn-mode))

;;;###autoload
(add-to-list 'interpreter-mode-alist '("bqn" . bqn-mode))

;;;; inferior BQN process

(define-obsolete-variable-alias 'bqn-comint-interpreter-path
  'bqn-interpreter "2023-04-14")
(defcustom bqn-interpreter "bqn"
  "Executable of the BQN interpreter for interactive use."
  :type 'string
  :group 'bqn)

(define-obsolete-variable-alias 'bqn-comint-interpreter-arguments
  'bqn-interpreter-arguments "2023-04-14")
(defcustom bqn-interpreter-arguments '()
  "Commandline arguments to pass to the BQN interpreter."
  :type 'string
  :group 'bqn)

(defvar bqn-comint-mode-map
  (let ((map (nconc (make-sparse-keymap) comint-mode-map)))
    ;; add keymaps here
    map)
  "Basic mode to run BQN.")

(defvar bqn-comint--process-name "BQN"
  "Name of BQN comint process.")

(defcustom bqn-comint-*process-buffer-name* "*BQN*"
  "Name of buffer which holds BQN process."
  :type 'string
  :group 'bqn)

(defcustom bqn-comint-flash-on-send t
  "When non-nil flash the region sent to BQN process."
  :type 'boolean
  :group 'bqn)

(defun bqn-comint--flash-region (start end &optional timeout)
  "Temporarily highlight region from START to END for TIMEOUT seconds."
  (let ((overlay (make-overlay start end)))
    (overlay-put overlay 'face 'secondary-selection)
    (run-with-timer (or timeout 0.2) nil 'delete-overlay overlay)))

(defun bqn-comint--ensure-process ()
  "Check for a running BQN process and return its buffer.
If the process does not exist, create it.

FIXME: we do not actually check that the process is alive."
  (if-let ((proc (get-process bqn-comint--process-name)))
      (process-buffer proc)
    (bqn-comint-run-process)))

;;;###autoload
(defun bqn-comint-run-process ()
  "Run an inferior BQN process inside Emacs and return its buffer."
  (interactive)
  (let ((buffer (comint-check-proc bqn-comint--process-name)))
    ;; pop to the "*BQN*" buffer when the process is dead, the buffer
    ;; is missing or it's got the wrong mode.
    (pop-to-buffer-same-window
     (if (or buffer (comint-check-proc (current-buffer)))
         (get-buffer-create (or buffer bqn-comint-*process-buffer-name*))
       (current-buffer)))
    ;; create the comint process unless there is a buffer already
    (unless buffer
      (setq buffer
            (apply #'make-comint-in-buffer
                   bqn-comint--process-name
                   buffer
                   bqn-interpreter bqn-interpreter-arguments))
      (switch-to-buffer-other-window bqn-comint-*process-buffer-name*)
      (bqn-comint-mode))
    buffer))

(defun bqn-comint--escape (str)
  ;; At least for CBQN, newlines in the string trigger immediate evaluation, so
  ;; use its escape mechanism.
  (concat
   ")escaped \""
   (with-temp-buffer
     (insert str)
     (goto-char (point-min))
     (while (search-forward-regexp "[\\\"\r\n]" nil 'noerror)
       (let ((m (match-string 0)))
         (cond
          ((string= m "\\") (replace-match "\\\\" t t))
          ((string= m "\"") (replace-match "\\\"" t t))
          ((string= m (char-to-string ?\n)) (replace-match "\\n" t t))
          ((string= m (char-to-string ?\r)) (replace-match "\\r" t t)))))
     (buffer-string))
   "\""))

(defun bqn-comint-process-execute-region (start end &optional follow)
  "Send the region bounded by START and END to the bqn-comint-process-session.

When FOLLOW is non-nil, switch to the inferior process buffer."
  (interactive "r")
  (when (= start end)
    (error "Attempt to send empty region to %s" bqn-comint--process-name))
  (when bqn-comint-flash-on-send
    (bqn-comint--flash-region start end))
  (let ((region (buffer-substring-no-properties start end))
        (pbuf (bqn-comint--ensure-process)))
    (with-current-buffer pbuf
      ;; get rid of prompt for output alignment
      (goto-char (point-max))
      (comint-kill-whole-line 0))
    (comint-send-string (get-buffer-process pbuf)
                        (concat (bqn-comint--escape region) "\n"))
    (when follow
      (select-window (display-buffer pbuf)))))

(defun bqn-comint-process-execute-line (&optional arg)
  "Send the active region, else the current line to the BQN process."
  (interactive "P")
  (cond
   ((use-region-p)
    (bqn-comint-process-execute-region (region-beginning) (region-end) arg))
   (t
    (bqn-comint-process-execute-region (line-beginning-position) (line-end-position) arg))))

(defun bqn-comint-process-execute-buffer (&optional arg)
  "Send the current buffer to BQN process."
  (interactive "P")
  (bqn-comint-process-execute-region (point-min) (point-max) arg))

(define-derived-mode bqn-comint-mode comint-mode "BQN interactive"
  "Major mode for inferior BQN processes."
  :syntax-table bqn-syntax--table
  (setq-local font-lock-defaults bqn-syntax--token-types)
  (buffer-face-set 'bqn-default))

(provide 'bqn-mode)

;;; bqn-mode.el ends here
